/*
 * Copyright (c) 2018-2021 NetFoundry, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'signing'

project.plugins.withType(MavenPublishPlugin).all {
    def pubExt = project.extensions.getByType(PublishingExtension)
    pubExt.publications.withType(MavenPublication).all { pub ->

        if (rootProject.hasProperty('signingKey')) {
            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            project.signing {
                useInMemoryPgpKeys(signingKey, signingPassword)
                sign pub
            }
        }

        pub.pom {
            name = project.getName()
            url = "https://openziti.org"
            description = project.getProperty('description')

            licenses {
                license {
                    name = "The Apache Software License, Version 2.0"
                    url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            developers {
                developer {
                    id = "advdev"
                    name = "NetFoundry Advanced Development Team"
                    email = "adv-dev@netfoundry.io"
                }
            }

            scm {
                connection = "scm:git:https://github.com/openziti/ziti-jvm-sdk"
                developerConnection = "scm:git:ssh://github.com/openziti/ziti-jvm-sdk.git"
                url = "https://github.com/openziti/ziti-sdk-jvm"
            }
        }
    }
}

