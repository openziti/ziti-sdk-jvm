import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

/*
 * Copyright (c) 2018-2021 NetFoundry, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

ext {
    description "Vertx Support for Ziti"
}

repositories {
    mavenCentral()
}

sourceSets {
    samples {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    samplesImplementation.extendsFrom(configurations.implementation)
    samplesRuntimeOnly.extendsFrom(configurations.runtimeOnly)
}

dependencies {
    api(project(':ziti'))
    implementation(project(':ziti-netty'))

    implementation deps.kotlin
    implementation "io.vertx:vertx-core:4.4.1"

    testApi deps.jupiterApi
    testImplementation deps.jupiter
    testImplementation deps.kotlinTest
    testImplementation deps.gson

    samplesImplementation "org.slf4j:slf4j-simple:+"
}

tasks.withType(KotlinCompilationTask).configureEach {
     kotlinOptions.jvmTarget = "11"
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
    classifier "sources"
}

tasks.register('dokkaJar', Jar) {
    classifier('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives(sourcesJar)
    archives(dokkaJar)
}

publishing {
    publications {
        zitiVertx(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact dokkaJar
        }
    }
}

apply from: rootProject.file('publish.gradle')
